from snakemake.io import temp, directory, service, from_queue
from snakemake.ioutils import branch
from snakemake.utils import min_version


min_version("9.1.1")
print("Starting grz-watchdog", file=sys.stderr)


container: "docker://condaforge/miniforge3:24.11.3-2"


configfile: "config/config.yaml"


include: "rules/common.smk"


# include: "rules/storage.smk"


GHGA_PUBLIC_KEY = "GHGA_PUBLIC_KEY"
GRZ_INTERNAL_PUBLIC_KEY = "GRZ_INTERNAL_PUBLIC_KEY"

INPUT_QUEUE.put("tmp/queue-workaround")


# This is a workaround for a bug (snakemake immediately exits)
# that occurs _sometimes_ when the queue is initially empty.
rule queue_initially_empty_workaround:
    output:
        temp("tmp/queue-workaround"),
    shell:
        """
        sleep 30s
        echo "done" > {output}
        """


# rule all:
#     input:
#         item=from_queue(INPUT_QUEUE, finish_sentinel=SENTINEL),
#         running="running",
#     default_target: True
#
#
# rule start:
#     output:
#         running=temp("running"),
#     run:
#         UPDATE_THREAD.start()
#         print("true", file=open(output.running, "wt"))


rule download_submission:
    """
    Download a submission from S3 to the local filesystem.
    Could probably also be done with s3 storage plugin.
    """
    input:
        s3_config="config/buckets/{bucket_name}.config.yaml",
    output:
        data=temp(directory("results/{bucket_name}/downloaded/{submission_id}")),
    log:
        "logs/{bucket_name}/download_submission/{submission_id}.log",
    conda:
        "envs/grzctl.yaml"
    shell:
        """
        (grzctl download --submission-id {wildcards.submission_id} --output-dir {output.data} --config-file {input.s3_config}) 2> {log}
        """


rule decrypt_submission:
    """
    Decrypt the downloaded submission using grzctl.
    """
    input:
        data="results/{bucket_name}/downloaded/{submission_id}",
        config="config/buckets/{bucket_name}.config.yaml",
    output:
        data=temp(directory("results/{bucket_name}/decrypted/{submission_id}")),
    log:
        "logs/{bucket_name}/decrypt_submission/{submission_id}.log",
    conda:
        "envs/grzctl.yaml"
    shell:
        """
        (grzctl decrypt --config-file {input.config} {input.data}) 2> {log}
        """


rule extract_metadata:
    """
    Extract metadata from the decrypted submission for some subsequent steps.
    """
    input:
        data="results/{bucket_name}/decrypted/{submission_id}",
    output:
        metadata=temp("results/{bucket_name}/metadata/{submission_id}.json"),
    log:
        "logs/{bucket_name}/extract_metadata/{submission_id}.log",
    conda:
        "envs/grzctl.yaml"
    shell:
        """
        (cp '{input.data}/metadata/metadata.json' {output.metadata}) 2> {log}
        """


rule validate_metadata:
    """
    Validate the submission using grzctl.
    """
    input:
        data="results/{bucket_name}/decrypted/{submission_id}",
    output:
        validation_flag=temp("results/{bucket_name}/validation_flag/{submission_id}"),
        validation_errors=temp(
            "results/{bucket_name}/validation_errors/{submission_id}.txt"
        ),
    log:
        "logs/{bucket_name}/validate_metadata/{submission_id}.log",
    conda:
        "envs/grzctl.yaml"
    shell:
        """
        (
            grzctl validate --submission-dir {input.data} 2> {output.validation_errors}
            if [ $? -eq 0 ]; then
                echo "true" > {output.validation_flag}
            else
                echo "false" > {output.validation_flag}
            fi
        ) 2> {log}
        """


rule check_consent:
    """
    Check if the submission has consent using grzctl.
    """
    input:
        data="results/{bucket_name}/decrypted/{submission_id}",
    output:
        consent_flag=temp("results/{bucket_name}/consent_flag/{submission_id}"),
    log:
        "logs/{bucket_name}/check_consent/{submission_id}.log",
    conda:
        "envs/grzctl.yaml"
    shell:
        """
        (grzctl consent --submission-dir {input.data} > {output.consent_flag}) 2> {log}
        """


rule re_encrypt_submission:
    """
    Re-encrypt the submission using the target public key, depending on whether research consent was given.
    """
    input:
        data="results/{bucket_name}/decrypted/{submission_id}",
        pruefbericht="results/{bucket_name}/pruefbericht/{submission_id}",
        consent_flag="results/{bucket_name}/consent_flag/{submission_id}",
        config_consented=config["buckets"]["consented"],
        config_nonconsented=config["buckets"]["nonconsented"],
    output:
        data=temp(directory("results/{bucket_name}/re-encrypt/{submission_id}")),
    log:
        "logs/{bucket_name}/re_encrypt_submission/{submission_id}.log",
    conda:
        "envs/grzctl.yaml"
    shell:
        """
        (
            CONSENT=$(cat {input.consent_flag})
            if [[ "$CONSENT" == "true" ]]; then
                CONFIG_FILE="{input.config_consented}"
            else
                CONFIG_FILE="{input.config_nonconsented}"
            fi
            echo "Consent: $CONSENT. Using config file: $CONFIG_FILE"
            grzctl encrypt --config-file "$CONFIG_FILE" --submission-dir {input.data} --output-dir {output.data}
        ) > {log} 2>&1
        """


rule archive_submission:
    """
    Upload data to the target s3 bucket.
    """
    input:
        data="results/{bucket_name}/re-encrypt/{submission_id}",
        consent_flag="results/{bucket_name}/consent_flag/{submission_id}",
        config_consented=config["buckets"]["consented"],
        config_nonconsented=config["buckets"]["nonconsented"],
    output:
        archived=temp("results/{bucket_name}/archived/{submission_id}"),
    log:
        "logs/{bucket_name}/archive_submission/{submission_id}.log",
    shell:
        """
        (
            CONSENT=$(cat {input.consent_flag})
            if [[ "$CONSENT" == "true" ]]; then
                CONFIG_FILE="{input.config_consented}"
            else
                CONFIG_FILE="{input.config_nonconsented}"
            fi
            echo "Consent: $CONSENT. Using config file for archiving: $CONFIG_FILE"
            grzctl archive --config-file "$CONFIG_FILE" TODO && echo 'done' > {output.archived}
        ) > {log} 2>&1
        """


rule generate_pruefbericht:
    """
    Generate a Prüfbericht for the submission.
    """
    input:
        validation_flag="results/{bucket_name}/validation_flag/{submission_id}",
        data="results/{bucket_name}/decrypted/{submission_id}",
    output:
        pruefbericht=temp("results/{bucket_name}/pruefbericht/{submission_id}"),
    log:
        "logs/{bucket_name}/generate_pruefbericht/{submission_id}.log",
    shell:
        """
        (
            IS_VALID=$(cat {input.validation_flag})
            if [[ "$IS_VALID" == "true" ]]; then
                pruefbericht_params=""
            else
                pruefbericht_params="--fail"
            fi
            grzctl pruefbericht --submission-dir {input.data} ${{pruefbericht_params}}
        ) > {log} 2>&1
        """


rule report_pruefbericht_to_bfarm:
    """
    Report the Prüfbericht to BfArM.
    """
    input:
        pruefbericht="results/{bucket_name}/pruefbericht/{submission_id}",
    output:
        answer=temp("results/{bucket_name}/pruefbericht_answer/{submission_id}"),
    log:
        "logs/{bucket_name}/report_pruefbericht_to_bfarm/{submission_id}.log",
    shell:
        """(echo 'Use BfArM API and record response' > {output.answer}) 2> {log}"""


checkpoint determine_if_qc_is_necessary:
    """
    Flip a biased coin to determine if QC is necessary and/or check submission DB
    """
    input:
        validation_flag="results/{bucket_name}/validation_flag/{submission_id}",
        pruefbericht="results/{bucket_name}/pruefbericht_answer/{submission_id}",
    output:
        needs_qc=temp("results/{bucket_name}/qc_flag/{submission_id}"),
    log:
        "logs/{bucket_name}/determine_if_qc_is_necessary/{submission_id}.log",
    shell:
        """
        (
            IS_VALID=$(cat {input.validation_flag})
            # TODO replace with query in submission DB and sophisticated logic
            if [[ "$IS_VALID" != "true" ]]; then
                echo "false" > {output.needs_qc}
            else
                if [ $(shuf -i 1-100 -n 1) -le 2 ]; then
                    echo "true" > {output.needs_qc}
                else
                    echo "false" > {output.needs_qc}
                fi
            fi
        ) > {log} 2>&1
        """


rule perform_qc:
    """
    Perform QC on the submission using the QC nextflow pipeline.
    """
    input:
        data="results/{bucket_name}/decrypted/{submission_id}",
    output:
        qc_results=temp("results/{bucket_name}/qc/{submission_id}"),
    # handover: "TODO: handover to QC pipeline"
    shell:
        "echo 'running fake qc...' > {output.qc_results}"


rule clean_submission_from_inbox:
    input:
        ready_marker=get_ready_marker,
    output:
        clean_results="results/{bucket_name}/clean_submission_from_inbox/{submission_id}",
    log:
        "logs/{bucket_name}/clean_submission_from_inbox/{submission_id}",
    shell:
        """
        (grzctl clean --submission-id {wildcards.submission_id} --yes-i-really-mean-it && echo 'true' > {output.clean_results}) 2> {log}
        """


rule update_taetigkeitsbericht:
    """
    Update the Taetigkeitsbericht with information from the respective submission.
    """
    input:
        metadata="results/{bucket_name}/metadata/{submission_id}.json",
        pruefbericht="results/{bucket_name}/pruefbericht/{submission_id}",
    output:
        taetigkeitsbericht=temp(
            "results/{bucket_name}/taetigkeitsbericht/{submission_id}.pdf"
        ),
    log:
        "logs/{bucket_name}/update_taetigkeitsbericht/{submission_id}.log",
    shell:
        """
        # (generate taetigkeitsbericht using {input.metadata} and {input.pruefbericht}, as well as some other data TBD) 2> {log}
        echo "TODO" > {output.taetigkeitsbericht} 2> {log}
        """


rule finalize_submission:
    """
    Target rule for each submission.
    This rule does not really do anything, except explicitly state what needs to happen (which inputs are required)
    in order for a submission to be considered "completed".
    """
    input:
        archived="results/{bucket_name}/archived/{submission_id}",
        pruefbericht="results/{bucket_name}/pruefbericht/{submission_id}",
        pruefbericht_answer="results/{bucket_name}/pruefbericht_answer/{submission_id}",
        taetigkeitsbericht="results/{bucket_name}/taetigkeitsbericht/{submission_id}.pdf",
        clean_results="results/{bucket_name}/clean_submission_from_inbox/{submission_id}",
    output:
        target="results/{bucket_name}/target/{submission_id}",
    log:
        "logs/{bucket_name}/finalize_submission/{submission_id}.log",
    shell:
        """
        (
            CLEANED=$(cat {input.clean_results})
            if [[ $"CLEANED" == "true" ]]; then
                echo "done" > {output.target}
            else
                echo "fail" > {output.target}
            fi
        ) > {log} 2>&1
        """
